buildscript {
    ext.kotlinVersion = "1.8.21"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.0.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("de.mannodermaus.gradle.plugins:android-junit5:1.9.3.0")
        classpath("com.adarshr:gradle-test-logger-plugin:3.2.0")
        classpath("digital.wup:android-maven-publish:3.6.3")
        classpath("info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.9.11")
    }
}

plugins {
    id("com.github.ben-manes.versions") version "0.46.0"
    id("io.spring.dependency-management") version "1.0.8.RELEASE"
}

allprojects {
    apply from: "$rootDir/.ops/git.gradle"
}

subprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    project.apply from: "$rootDir/dependencies.gradle"

    afterEvaluate { project ->
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                freeCompilerArgs += ["-progressive"]
            }
        }

        if (plugins.hasPlugin("kotlin")) {
            java {
                sourceCompatibility = JavaVersion.VERSION_17
                targetCompatibility = JavaVersion.VERSION_17
            }

            compileKotlin {
                kotlinOptions {
                    jvmTarget = "17"
                }
            }
        }

        if (plugins.hasPlugin("com.android.library") || plugins.hasPlugin("com.android.application")) {
            java {
                sourceCompatibility = JavaVersion.VERSION_17
                targetCompatibility = JavaVersion.VERSION_17
            }

            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }

                compileSdkVersion(33)
                defaultConfig {
                    targetSdkVersion(33)

                    versionName = git.versionName()
                    versionCode = git.commitCount()

                    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
                }

                testOptions {
                    unitTests.returnDefaultValues = true
                }

                lintOptions {
                    warningsAsErrors = true
                    textReport = true

                    ignoreTestSources true

                    ignore "ObsoleteLintCustomCheck", "GradleDependency"
                }
            }

            if (project.hasProperty("publishing")) {
                project.apply from: "$rootDir/.ops/publishing-android.gradle"
            }
        } else {
            if (project.hasProperty("publishing")) {
                project.apply from: "$rootDir/.ops/publishing-kotlin.gradle"
            }
        }

        if (project.name != "docs" && (project.hasProperty("test") || project.hasProperty("android"))) {
            project.apply from: "$rootDir/.ops/testing.gradle"
            project.apply from: "$rootDir/.ops/pitest.gradle"
        }
    }
}

task clean(type: Delete) {
    delete(rootProject.buildDir)
}

println("Version ${git.versionName()}")
