publishing {
    def sonatype_username = "${findProperty('sonatype_username') ?: System.getenv("sonatype_username")}"
    def sonatype_password = "${findProperty('sonatype_password') ?: System.getenv("sonatype_password")}"

    repositories {
        maven {
            name = "Sonatype"
            setUrl("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = sonatype_username
                password = sonatype_password
            }
        }

        maven {
            name = "SonatypeSnapshots"
            setUrl("https://oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = sonatype_username
                password = sonatype_password
            }
        }
    }

    publications {
        maven(MavenPublication) {
            versionMapping {
                usage("java-api") {
                    fromResolutionResult()
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }

            version = git.versionName()

            groupId = project.getProperty("groupId")
            artifactId = project.getProperty("artifactId")

            pom {
                name = "Acorn"
                description = "Mastering Android Navigation"
                url = "https://github.com/nhaarman/Acorn"
                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0"
                    }
                }

                developers {
                    developer {
                        id = "nhaarman"
                        name = "Niek Haarman"
                    }
                }

                scm {
                    connection = 'scm:git@github.com:nhaarman/Acorn.git'
                    developerConnection = 'scm:git@github.com:nhaarman/Acorn.git'
                    url = 'https://github.com/nhaarman/Acorn'
                }
            }
        }
    }
}

tasks.withType(PublishToMavenRepository).configureEach {
    onlyIf {
        git.versionName().contains("SNAPSHOT") && repository == publishing.repositories["SonatypeSnapshots"] ||
                !git.versionName().contains("SNAPSHOT") && repository == publishing.repositories["Sonatype"]
    }
}

signing {
    if (findProperty("signing.keyId") != null) {
        sign(publishing.publications["maven"])
    } else {
        println "No signing keyId found, skipping signing."
    }
}